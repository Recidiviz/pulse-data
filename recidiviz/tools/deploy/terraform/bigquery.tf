# Recidiviz - a data platform for criminal justice reform
# Copyright (C) 2020 Recidiviz, Inc.
#
# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <https://www.gnu.org/licenses/>.
# =============================================================================

locals {
  source_tables = "${local.recidiviz_root}/source_tables/externally_managed/"
}

resource "google_project_service" "bigquery_connection_api" {
  service = "bigqueryconnection.googleapis.com"

  disable_dependent_services = true
  disable_on_destroy         = true
}

# Storing validation results
module "validation_results_dataset" {
  source      = "./modules/big_query_dataset"
  dataset_id  = "validation_results"
  description = "This dataset contains raw results from data validation runs as well as any views over them."
}

resource "google_bigquery_table" "validation_results" {
  dataset_id          = module.validation_results_dataset.dataset_id
  table_id            = "validation_results"
  description         = "This table contains the results from data validation runs."
  deletion_protection = false

  schema = jsonencode(yamldecode(file("${local.source_tables}/validation_results/validation_results.yaml"))["schema"])
}

module "supplemental_generated_dataset" {
  source      = "./modules/big_query_dataset"
  dataset_id  = "supplemental_data"
  description = "This dataset contains tables with generated data that does not go through the traditional ingest and calc pipelines."
}

module "export_archives_dataset" {
  source      = "./modules/big_query_dataset"
  dataset_id  = "export_archives"
  description = "This dataset contains tables that archive the contents of daily exports."
}

module "twilio_webhook_requests_dataset" {
  source      = "./modules/big_query_dataset"
  dataset_id  = "twilio_webhook_requests"
  description = "This dataset contains tables that archive requests made by Twilio to Recidiviz resources."
}

module "raw_data_comparison_output" {
  source                      = "./modules/big_query_dataset"
  dataset_id                  = "raw_data_comparison_output"
  description                 = "This dataset contains tables that store the output of raw data comparisons generated by compare_raw_data.py."
  default_table_expiration_ms = 1000 * 60 * 60 * 24 * 7 # 7 days
}

resource "google_bigquery_table" "jii_texting_incoming_messages" {
  dataset_id          = module.twilio_webhook_requests_dataset.dataset_id
  table_id            = "jii_texting_incoming_messages"
  description         = "This table contains information from Twilio's requests to our webhook that handles incoming text messages to our JII Texting Twilio phone numbers"
  deletion_protection = false
  schema              = jsonencode(yamldecode(file("${local.source_tables}/${module.twilio_webhook_requests_dataset.dataset_id}/jii_texting_incoming_messages.yaml"))["schema"])
}
